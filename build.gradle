task createWrapper(type: Wrapper) {
  gradleVersion = '5.0'
}

buildscript
{
	ext
	{
		springBootVersion = '2.1.1.RELEASE'
	}
	repositories
	{
		mavenLocal()
		jcenter()
		maven { url "https://repo.spring.io/milestone" }
	}
	dependencies
	{
		classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
		classpath "org.asciidoctor:asciidoctor-gradle-plugin:1.5.6"
	}
}

apply plugin: 'java'
//eclipse
//apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.asciidoctor.convert'

group               = 'org.btr'
version             = '1.0'
sourceCompatibility = 11
targetCompatibility = 11

repositories
{
	mavenLocal()
	jcenter()
	maven { url "https://repo.spring.io/milestone" }
}

ext
{
	vavrVersion = '0.9.2'
	snippetsDir = file("build/generated-snippets")
}

asciidoctor
{
	dependsOn test
	inputs.dir snippetsDir
}

bootJar
{
	dependsOn asciidoctor
	from("${asciidoctor.outputDir}/html5")
	{
		into 'static/docs'
	}
}

configurations
{
	// 全局去除tomcat
	compile.exclude module: 'spring-boot-starter-tomcat'
}

dependencies
{
	// Java8函数式增强
	compile "io.vavr:vavr:${vavrVersion}",
	        "io.vavr:vavr-jackson:${vavrVersion}",
	        // SpringWeb支持
	        "org.springframework.boot:spring-boot-starter-web",
	        // Jpa
	        "org.springframework.boot:spring-boot-starter-data-jpa",
	        // Rest第三级超文本驱动
	        "org.springframework.boot:spring-boot-starter-hateoas",
	        // 简化代码
	        "org.projectlombok:lombok"
	// H2测试用数据库
	runtimeOnly "com.h2database:h2",
	            "mysql:mysql-connector-java"
	testCompile "org.springframework.boot:spring-boot-starter-test",
	            "io.projectreactor:reactor-test",
	            "org.springframework.restdocs:spring-restdocs-mockmvc"
	// 生成api文档
	asciidoctor "org.springframework.restdocs:spring-restdocs-asciidoctor:2.0.2.RELEASE"
}
